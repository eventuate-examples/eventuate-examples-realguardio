plugins {
    id 'java'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

group = 'com.realguardio'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
    maven { url 'https://snapshots.repositories.eventuate.io/repository' }
    mavenLocal()
}

sourceSets {
    endToEndTest {
        java {
            srcDir 'src/endToEndTest/java'
        }
        resources {
            srcDir 'src/endToEndTest/resources'
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    endToEndTestImplementation.extendsFrom implementation
    endToEndTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    // Test Framework
    endToEndTestImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    endToEndTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    // RestAssured for API Testing
    endToEndTestImplementation 'io.rest-assured:rest-assured:5.3.2'
    
    // Testcontainers
    endToEndTestImplementation 'org.testcontainers:testcontainers:1.19.1'
    endToEndTestImplementation 'org.testcontainers:junit-jupiter:1.19.1'
    
    // Eventuate Testcontainers support
    endToEndTestImplementation platform('io.eventuate.platform:eventuate-platform-dependencies:2025.1.BUILD-SNAPSHOT')
    endToEndTestImplementation 'io.eventuate.common:eventuate-common-testcontainers'
    endToEndTestImplementation 'io.eventuate.messaging.kafka:eventuate-messaging-kafka-testcontainers'
    endToEndTestImplementation "io.eventuate.platform.testcontainer.support:eventuate-platform-testcontainer-support-service:$eventuatePlatformTestContainerSupportVersion"
    endToEndTestImplementation "io.eventuate.cdc:eventuate-cdc-testcontainers"

    endToEndTestImplementation "io.eventuate.examples.spring-authorization-server:authorization-server-test-containers:0.1.0.BUILD-SNAPSHOT"
    endToEndTestImplementation 'org.springframework.boot:spring-boot-starter-webflux:3.4.1'
    endToEndTestImplementation 'io.realguardio.osointegration:oso-service-test-container:0.1.0-SNAPSHOT'

    // Logging
    endToEndTestImplementation 'org.slf4j:slf4j-api:2.0.9'
    endToEndTestImplementation 'ch.qos.logback:logback-classic:1.4.11'
    
    // Assertions
    endToEndTestImplementation 'org.assertj:assertj-core:3.24.2'
    
    // Awaitility for polling/waiting
    endToEndTestImplementation 'org.awaitility:awaitility:4.2.0'
    
    // JSON Processing
    endToEndTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'
    
    // Unit testing for DTOs
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'

    // Main dependencies for DTOs
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'
}

task endToEndTest(type: Test) {
    description = 'Runs end-to-end tests'
    group = 'verification'
    
    testClassesDirs = sourceSets.endToEndTest.output.classesDirs
    classpath = sourceSets.endToEndTest.runtimeClasspath
    
    useJUnitPlatform()

    systemProperty "eventuate.servicecontainer.serviceimage.version", project.version

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    
    // Increase heap size for Testcontainers
    maxHeapSize = '2g'
    jvmArgs '-XX:MaxMetaspaceSize=512m'
}

test {
    useJUnitPlatform()
}

check.dependsOn endToEndTest

endToEndTest {
    systemProperty "spring.profiles.active", "test,postgres"
    if (project.hasProperty('endToEndTestMode'))
        systemProperty "endToEndTestMode", endToEndTestMode
}

task assembleServices {
    description = 'Assembles all dependent services'
    group = 'build setup'
}

['realguardio-orchestration-service', 'realguardio-customer-service', 'realguardio-security-system-service', 'realguardio-oso-integration-service'].each { serviceDir ->
    def taskName = "assemble${serviceDir.replaceAll(/(^\w|-\w)/) { it[0].toUpperCase() }.replaceAll('-', '') }"
    task "$taskName"(type: GradleBuild) {
        dir = "../$serviceDir"
        tasks = ['assemble']
    }
    assembleServices.dependsOn taskName
    endToEndTest.dependsOn taskName

}

task publishOsoIntegrationServiceToMavenLocal(type: GradleBuild) {
    description = 'Publishes OSO integration service to Maven Local'
    group = 'build setup'
    dir = '../realguardio-oso-integration-service'
    tasks = ['publishToMavenLocal']
}

tasks.named('compileEndToEndTestJava') {
    dependsOn publishOsoIntegrationServiceToMavenLocal
}

task dockerComposeUp(type: Exec) {
    description = 'Starts all services using docker compose'
    group = 'application'

    dependsOn "assembleServices"

    if (project.hasProperty('endToEndTestNoBuild'))
        commandLine 'docker', 'compose', 'up', '-d', '--wait'
    else
        commandLine 'docker', 'compose', 'up', '-d', '--build', '--wait'

    // Optional: Set the working directory if docker-compose.yml is not in the project root
    // workingDir '../'
}

task dockerComposeBuild(type: Exec) {
    description = 'Starts all services using docker compose'
    group = 'application'

    dependsOn "assembleServices"

    commandLine 'docker', 'compose', 'build'

    // Optional: Set the working directory if docker-compose.yml is not in the project root
    // workingDir '../'
}
if (project.hasProperty('endToEndTestMode') && endToEndTestMode == 'DockerCompose')
    endToEndTest.dependsOn dockerComposeUp

