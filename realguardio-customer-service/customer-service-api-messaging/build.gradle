apply plugin: 'java-test-fixtures'

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

dependencies {
    implementation project(':customer-service-domain')
    implementation project(':customer-service-persistence')
    
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'io.eventuate.tram.core:eventuate-tram-spring-events'
    implementation 'io.eventuate.tram.core:eventuate-tram-spring-commands'
    implementation 'io.eventuate.tram.sagas:eventuate-tram-sagas-spring-participant'
    implementation 'io.eventuate.tram.core:eventuate-tram-spring-consumer-kafka'
    implementation 'io.eventuate.tram.core:eventuate-tram-spring-producer-jdbc'

    testImplementation "io.eventuate.tram.core:eventuate-tram-spring-commands-starter"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'io.eventuate.tram.sagas:eventuate-tram-sagas-spring-in-memory'
    testImplementation 'io.eventuate.tram.core:eventuate-tram-test-util'
    testRuntimeOnly 'com.h2database:h2'
    
    integrationTestImplementation 'io.eventuate.common:eventuate-common-testcontainers'
    integrationTestImplementation 'io.eventuate.messaging.kafka:eventuate-messaging-kafka-testcontainers'
    integrationTestImplementation 'io.eventuate.tram.core:eventuate-tram-spring-flyway'
    integrationTestImplementation 'io.eventuate.util:eventuate-util-test'
    integrationTestImplementation 'org.testcontainers:testcontainers'
    integrationTestImplementation 'org.testcontainers:postgresql'
    integrationTestRuntimeOnly 'org.postgresql:postgresql'
}

tasks.register('integrationTest', Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
}

tasks.named('processIntegrationTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}